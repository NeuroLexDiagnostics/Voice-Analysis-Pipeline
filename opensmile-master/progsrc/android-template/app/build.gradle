apply plugin: 'com.android.application'
apply plugin: "jp.leafytree.android-scala"

android {
    compileSdkVersion 23
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.audeering.opensmile.androidtemplate"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            scala {
                srcDir "src/main/scala"
            }
            jniLibs.srcDir 'src/main/libs'
            jni.srcDirs = [] //disable automatic ndk-build call
        }
        androidTest {
            scala {
                srcDir "src/androidTest/scala"
            }
        }
    }

    project.ext.versionCodes = ['armeabi': 1]

    //versionCode digit for each supported ABI, with 64bit>32bit and x86>armeabi-*
    android.applicationVariants.all { variant ->
        // assign different version code for each output
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) * 1000000 + defaultConfig.versionCode
        }
    }

    // call regular ndk-build(.cmd) script from app directory
    task ndkBuild(type: Exec) {
        commandLine '/path/to/android/ndk-r10e/ndk-build', 'NDK_DEBUG=1', 'TARGET_PLATFORM=android-21', '-C', file('src/main').absolutePath
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'org.scala-lang:scala-library:2.11.7'
    compile 'org.json:json:20160212'
    compile 'javax.vecmath:vecmath:1.5.2'
}
